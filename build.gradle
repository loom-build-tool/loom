apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'

group = 'jobt'
archivesBaseName = 'jobt'
version = '1.0.0'

sourceCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    compile project(':api')
    compile project(':plugin-java')
    compile project(':plugin-checkstyle')
    compile('org.sonatype.aether:aether-impl:1.13.1')
    compile('org.sonatype.aether:aether-connector-wagon:1.13.1')
    compile('org.sonatype.aether:aether-connector-file:1.13.1')
    compile('org.apache.maven.wagon:wagon-http-lightweight:2.12')
    compile('org.apache.maven:maven-aether-provider:3.0.3')
    compile('org.yaml:snakeyaml:1.18')
    compile('commons-cli:commons-cli:1.4')
    testCompile('junit:junit:4.12')
}

checkstyle {
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
    configProperties = [samedir: "${rootDir}/config/checkstyle", project_loc: "${rootDir}"]
}

pmd {
    consoleOutput = true
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

task zip(type: Zip) {
    into("lib") {
        from configurations.runtime.allArtifacts.files
        from configurations.runtime
    }
    into("scripts") {
        from 'scripts/build.yml'
        from 'scripts/jobt'
    }
}
